
#include <Arduino.h>
#include <ctype.h>
#include <stdbool.h>
#include <stddef.h>
#include <stdint.h>

/*
v1.4 08/2026 nouveau core, trame temp variable
v1.3 05/2026 board upesy, humidite absolue, envoi vers serveur
v1.2 03/2026 Surveillance batterie, log 24h en eeprom, OTA à la demande
v1.1 03/2026 copie de plat_esp_chad_gar v1.11 de 3/2026
*/

#include "variables.h"
#include <WiFi.h>
#include <esp_wifi.h>
#include <esp_now.h>
#include <Preferences.h>  // pour nvs eeprom
#include <PID_v1.h>
#include <DHT.h>
#include <ArduinoJson.h>
#include <HTTPClient.h>
#include "ClosedCube_HDC1080.h"
#include "Wire.h"

extern WiFiClient client;
extern Preferences preferences_nvs;  // Déclaration externe

RTC_NOINIT_ATTR unsigned long envoi_min_prec=0, Mesure_min_prec=0;
RTC_NOINIT_ATTR float TintPrec=20;


// variables Detection PIR
RTC_NOINIT_ATTR uint16_t compteur_detection=0;
RTC_NOINIT_ATTR uint16_t compteur_detection_1h=0;

RTC_NOINIT_ATTR uint8_t pause_detection;
RTC_NOINIT_ATTR unsigned long last_detection_time=0;

RTC_NOINIT_ATTR uint16_t Nb_PI[NB_VAL_TAB];

RTC_NOINIT_ATTR uint8_t  WIFI_CHANNEL;
RTC_NOINIT_ATTR uint8_t etat_now;
RTC_NOINIT_ATTR uint16_t Seuil_batt_sonde;  // millivolt
RTC_NOINIT_ATTR uint8_t Nb_jours_Batt_log;
RTC_NOINIT_ATTR uint8_t freq_envoi, cpt_mesure, cpt_nb_val;
RTC_NOINIT_ATTR uint16_t valTemp[NB_VAL_TAB], valHum[NB_VAL_TAB], valEcart[NB_VAL_TAB];

RTC_NOINIT_ATTR uint8_t compteur_graph;
RTC_NOINIT_ATTR uint16_t compteur_24h;

RTC_NOINIT_ATTR float PIR_24h=0;
RTC_NOINIT_ATTR uint16_t cpt24_PIR=0, PIRV=0;

RTC_NOINIT_ATTR uint8_t Capt_tps_max;
RTC_NOINIT_ATTR uint8_t Capt_seuil_temp;
RTC_NOINIT_ATTR uint8_t Capt_nb_val_max;
RTC_NOINIT_ATTR uint16_t Capt_tps_total_max;
RTC_NOINIT_ATTR uint8_t delai_detection;

RTC_NOINIT_ATTR uint8_t etat_ESP_stop;  // 0:normal 1:arrêté pour batterie faible (deepsleep longue duree)
RTC_NOINIT_ATTR uint8_t cpt24h_batt;


RTC_NOINIT_ATTR uint8_t mac_gw[6];   // B0:CB:D8:E9:0C:74  adresse mac esp_dest
volatile uint8_t ackReceived = false;  // global pour indiquer que le peer a acké
volatile int ackChannel = -1;       // canal où ça a marché

extern uint16_t nb_err_reseau;
extern  uint16_t TextV, TintV, HumV, HAV;  // pour stockage dans la partition log_flashG


//void OnDataRecv(const esp_now_recv_info_t *info, const uint8_t *data, int len);
void OnDataRecv(const uint8_t *mac, const uint8_t *data, int len);
//void OnDataRecv(const esp_now_peer_info_t * info, const uint8_t *incomingData, int len);

#if ESP_ARDUINO_VERSION_MAJOR >= 3
  void OnDataSent(const wifi_tx_info_t* info, esp_now_send_status_t status);
#else
  void OnDataSent(const uint8_t* mac_addr, esp_now_send_status_t status);
#endif

uint8_t parseMacString(const char* str, uint8_t mac[6]);

uint8_t envoi_now(uint8_t channel, esp_now_peer_info_t * peerInfo, Message_EspNow *message);
uint8_t envoi_data_gateway(Message_EspNow mess_esp);


#ifdef Temp_int_DS18B20
  OneWireNg_CurrentPlatform ow(PIN_DS18B20, false);
  OneWireNg_DS18B20 sensor(&ow);

  // Capteur temperature Dallas DS18B20  Temperature intérieure
  typedef uint8_t DeviceAddress[8];
  const int PIN_Tint = 13;      // Tint:Entrée onewire GPIO DS18B20
  //OneWire oneWire(PIN_Tint);
  //DallasTemperature ds(&oneWire);
  int nb_capteurs_temp = 1;  //DS18B20
  DeviceAddress Thermometer[5];
  DeviceAddress adds;
#endif

DHT dht[] = {
  { PIN_Tint22, DHT22 },
};
#ifdef Temp_int_HDC1080
  ClosedCube_HDC1080 hdc1080;
#endif

// Temperature intérieure
float Tint, Text, Humid;
RTC_NOINIT_ATTR uint16_t err_Tint, err_Text, err_Heure;  // compteurs d'erreurs

RTC_NOINIT_ATTR uint16_t calib_hygro1=300, calib_hygro2=800, calib_temp=1000; // calibration hygrométrie HDC1080



// ---------FONCTIONS DEFINIES AILLEURS -----------------

int readLastLogsG(int nombre);



// ----------  FONCTIONS APPLI --------------

void init_10_secondes()
{
}

//setup au debut
void setup_0()
{

  #ifdef ESP32_uPesy
    pinMode(PIN_Vbatt, INPUT);
  #endif

  /*if (NB_Graphique==6)
  {
    graphique[0][0] = 180;  //Tint - vert
    graphique[1][0] = 185;
    graphique[2][0] = 190;

    graphique[0][1] = 110;  // Text - bleu
    graphique[1][1] = 80;
    graphique[2][1] = 103;
    graphique[3][1] = 95;

    graphique[0][2] = 150;  // Chaud
    graphique[1][2] = 150;
    graphique[2][2] = 200;
    graphique[3][2] = 200;

    graphique[0][3] = 185;  // Tint moy
    graphique[1][3] = 183;
    graphique[2][3] = 183;
    graphique[3][3] = 195;

    graphique[0][4] = 35;   // Text moy
    graphique[1][4] = 38;
    graphique[2][4] = 42;
    graphique[3][4] = 32;

    graphique[0][5] = 50;  // cout
    graphique[1][5] = 55;
    graphique[2][5] = 48;  
    graphique[3][5] = 52;
  }*/
}



// setup apres la lecture nvs, avant démarrage reseau
void setup_1()
{

    Tint = 15;
    #ifdef Temp_int_HDC1080
      //delay(200);
      //i2cBootRecovery();
      Wire.begin(PIN_SDA, PIN_SCL); // Forçage des pins SDA=8, SCL=9 pour ESP32 S3 DevKit V1

      hdc1080.begin(0x40);
      /*if (i2cDevicePresent(0x40)) {
        Serial.println("HDC1080 détecté");
        hdc1080.begin(0x40);
      } else {
        Serial.println("HDC1080 ABSENT");
      }*/
    #endif

  // initialisation capteur de température intérieur
    #ifdef Temp_int_DHT22
      dht[0].begin();
    #endif
 
 
    #ifdef Temp_int_DS18B20
      ds.begin();  // Startup librairie DS18B20
      nb_capteurs_temp = ds.getDeviceCount();
      Serial.print("Nb Capteurs DS18B20:");
      Serial.println(nb_capteurs_temp);
      if (nb_capteurs_temp > 1) nb_capteurs_temp = 1;
      int j;
      for (j = 0; j < nb_capteurs_temp; j++) {
        Serial.print(" Capteur :");
        ds.getAddress(Thermometer[j], j);
        printAddress(Thermometer[j]);
      }
    #endif

    // lecture initiale temperature interieure
    /*uint8_t Tint_err = lecture_Tint(&Tint);
    if ((Tint < 1) || (Tint > 45)) {
      Tint = 20.0;
      Tint_err = 7;
    }
    if (Tint_err) log_erreur(Code_erreur_Tint, Tint_err, 1);
    else
      Serial.printf("Temp int:%.2f\n\r", Tint);*/

}

// apres demarrage reseau
void setup_2()
{


  #ifdef ESP_TJ_ACTIF

    // lecture des données sauvegardées dans la partition log_flashG
    readLastLogsG(99);

    // Configuration WiFi en mode Station pour ESP-NOW

    if ((mode_reseau==13) )
      WiFi.mode(WIFI_STA);
    
    // 🔍 DIAGNOSTIC: Forcer le canal WiFi
    uint8_t current_channel;
    wifi_second_chan_t second;
    esp_wifi_get_channel(&current_channel, &second);
    Serial.printf("Canal WiFi AVANT config ESP-NOW: %d\n", current_channel);
    
    // Forcer le canal si nécessaire (doit correspondre au routeur)
    // esp_wifi_set_promiscuous(true);
    // esp_wifi_set_channel(WIFI_CHANNEL, WIFI_SECOND_CHAN_NONE);
    // esp_wifi_set_promiscuous(false);
    
    if (esp_now_init() != ESP_OK) {
      Serial.println("Erreur initialisation ESP-NOW");
      return;
    }
    esp_now_register_recv_cb(OnDataRecv);
    
    // Vérifier le canal après init
    esp_wifi_get_channel(&current_channel, &second);
    
    Serial.println("\n\n======================================");
    Serial.println("🔵 ESP-NOW Initialisé (RÉCEPTEUR)");
    Serial.print("   MAC Address module: ");
    //if ((mode_reseau==13) )
    //else
    //  Serial.println(WiFi.softAPmacAddress());
    Serial.println(WiFi.macAddress());

    // Stockage de l'adresse MAC dans le tableau mac_gw[6]
    Serial.printf("   MAC dest : %02X:%02X:%02X:%02X:%02X:%02X\n",
            mac_gw[0], mac_gw[1], mac_gw[2],
            mac_gw[3], mac_gw[4], mac_gw[5]);


    Serial.printf("   Canal WiFi: %d\n", current_channel);
    Serial.println("   En attente de messages...");
    Serial.println("======================================\n\n");
    delay(2000); // 2 secondes de pause pour lire

  #else  // Si veille
      readLastLogsG(99);

  #endif
}

void setup_appli()
{
  
  // -------------  Capteur/Detecteur : stockage ou envoie infos à la gateway -------------------

  if (type_reveil ==2)   // Detection PIR
  {
    detection_pir();
    uint64_t sleep_time = (uint64_t)periode_cycle * 60 * 1000000;
    if (mode_rapide==12)
    sleep_time = (uint64_t)periode_cycle * 1000000;
    passage_deep_sleep( sleep_time); // 30ULL * 1000000ULL);
  }
  else 
  {
    if (type_reveil == 1)  event_cycle(); // réveil par timer
    else 
    {
      if (log_detail>=2) Serial.printf("milli H1: %lu\n", millis());
      envoi_temp_hygro();  // 30ms
       if (log_detail>=2) Serial.printf("milli H3: %lu\n", millis());
    }
    uint8_t etat_BTN0 = digitalRead(BTN_PIN[0]);  // repos-pull_up => 0, appui => 1
    if (log_detail>=2)Serial.printf("Etat BTN0: %i  type_reveil:%i\n\r", etat_BTN0, type_reveil);
    if ((type_reveil != 4) || (!etat_BTN0))  // reveil par BTN0 encore appuyé => pas sleep
    {
      uint64_t sleep_time = (uint64_t)periode_cycle * 60 * 1000000;
      if (mode_rapide==12)
      sleep_time = (uint64_t)periode_cycle * 1000000;
      passage_deep_sleep( sleep_time); // 30ULL * 1000000ULL);
    }
  }

}

void init_ram_variables_appli()
{
  cpt24h_batt=6;   // compteur de jours
}

void appli_event_on(systeme_eve_t evt)
{
  Serial.printf("Evenement on : %i\n\r", evt.data);

  if (evt.data == 1)  // bouton 1 appuyé
  {
      envoi_temp_hygro();
  }
}

void detection_pir()
{
    compteur_detection++;  // nb de detection des 5 dernières minutes
    compteur_detection_1h++; // nb de detection de la dernière heure
    if (compteur_detection_1h == 1) // premiere detection du cyle 1h
    {
      writeLog('D', 1, 0, 0, "PIR");
      // envoi d'un sms par http pour prevenir d'une presence
    }
}


// log batterie et Temp moyenne toutes les 24h.
void enreg_24h( uint8_t veille)
{
    // Log toutes les jours/semaines : nb d'erreurs wifi et batteries
  if (Nb_jours_Batt_log)
  {
    float vbatt = readBatteryVoltage();
    Serial.printf("Tension Batterie : %.2f V\n", vbatt);

    if ((Seuil_batt_arret_ESP) && (vbatt < Seuil_batt_arret_ESP) && (vbatt > 2400))
    {
      writeLog('S', 7, 0, 0, "Batt Stp");
      etat_ESP_stop = 1;
    }
    cpt24h_batt++;
    if (cpt24h_batt >= Nb_jours_Batt_log)  // log chaque X jour 
    {
      cpt24h_batt=0;

      //Serial.printf("Tension Batterie 24h : %.2f %.2f V\n", vbatt, Vbatt_Th);
      if (veille)
      {
        writeLog('K', (uint8_t)((vbatt-2.0)*100), 0, 0, "24H_Res");
      }
      else
      {
        if (nb_err_reseau>255) nb_err_reseau=255;
        writeLog('K', (uint8_t)((vbatt-2.0)*100), 0, (uint8_t)nb_err_reseau, "24H_Res");
        nb_err_reseau=0;
      }
    }
  }

  // erreurs Tint, Text, heure
  if (err_Tint>254) err_Tint=255;
  if (err_Tint)
            log_erreur(Code_erreur_Tint, err_Tint,0);
  if (err_Text>254) err_Text=255;
  if (err_Text)
            log_erreur(Code_erreur_Text, err_Text,0);
  if (err_Heure>254) err_Heure=255;
  if (err_Heure)
            log_erreur(Code_erreur_Heure, err_Heure,0);
  err_Tint=0;
  err_Text=0;
  err_Heure=0;

  // graphique des temperatures quotidiennes
  uint16_t tempI=1, tempE=1, Hum=1, HA=1, PIR=1;
  if (cpt24_Tint)  tempI = (uint16_t)(tempI_moy24h/cpt24_Tint*10);
  if (cpt24_Text)  tempE = (uint16_t)(tempE_moy24h/cpt24_Text*10);
  if (cpt24_Hum) Hum = (uint16_t)(Hum_24h/cpt24_Hum*10);
  if (cpt24_HA) HA = (uint16_t)(HA_moy24h/cpt24_HA*10);
  if (cpt24_PIR) PIR = (uint8_t)(PIR_24h/cpt24_PIR*10);
  if (!tempI) tempI=1;  // permet d'afficher quand meme le point sur le graphique
  if (!tempE) tempE=1;  // permet d'afficher quand meme le point sur le graphique
  if (!Hum) Hum=1;  // permet d'afficher quand meme le point sur le graphique
  if (!HA) HA=1;  // permet d'afficher quand meme le point sur le graphique
  if (!PIR) PIR=1;  // permet d'afficher quand meme le point sur le graphique
  TextV = tempE;
  TintV = tempI;
  HumV = Hum;
  HAV = HA;
  PIRV = PIR;

  //Serial.printf("Temp24h I:%.2f %i E:%.2f %i C:%.2f %i\n\r", tempI_moy24h, cpt24_Tint, tempE_moy24h, cpt24_Text, cout_moy24h, cpt24_Cout);

  tempI_moy24h=0;
  tempE_moy24h=0;
  Hum_24h=0;
  HA_moy24h=0;
  PIR_24h=0;
  cpt24_Tint=0;
  cpt24_Text=0;
  cpt24_Hum=0;
  cpt24_HA=0;
  cpt24_PIR=0;

  uint8_t i;
  for (i = NB_Val_Graph - 1; i; i--) {
    graphique[i][3] = graphique[i - 1][3];
    graphique[i][4] = graphique[i - 1][4];
    graphique[i][5] = graphique[i - 1][5];
  }
  graphique[0][3] = tempI;
  graphique[0][4] = selec_graph(GRAPH2, HA, Hum, PIR);
  graphique[0][5] = selec_graph(GRAPH3, HA, Hum, PIR);  

  Serial.printf("Graphique 24h : Tint:%i Text:%i Hum:%i HA:%i\n\r", tempI, tempE, Hum, HA);
  writeLogG('G', tempI, HA, Hum); // Enregistrment en Flash des 3 valeurs du graphique

  if (action_envoi && esp_now_actif)
  {
    Message_EspNow message;

    message.destinataire = SERVER_ADD & 0x80;  // 0x80 = message hexa
    message.emetteur = ADDRESS;
    message.code = 'C';
    message.code2 = 'J';

    uint8_t pos = 0;
    payloadWrite(message.payload, pos, tempI);
    payloadWrite(message.payload, pos, Hum);
    message.longueur = 5 + pos - 3;

    uint8_t result = envoi_data_gateway(message);

  }      
}


void appli_event_off(systeme_eve_t evt)
{
  // Detecteur PIR activé
  if (evt.data == 1)
  {
    detection_pir();
  }
  Serial.printf("Evenement off : %i\n\r", evt.data);
}

char* requete_status_appli(char *p)
{
  p += sprintf(p, "\"PIR_D\":%i,", compteur_detection); // derniere 15min
  p += sprintf(p, "\"PIR_V\":%i,", PIRV); // veille
  return p;
}

// type 1
uint8_t requete_Get_appli(const char* var, float *valeur)
  //uint8_t requete_Get_appli (String var, float *valeur) 
{
  uint8_t res=1;

  if (strncmp(var, "Tint",5) == 0) {
    res = 0;
    *valeur = Tint;
  }
  if (strncmp(var, "Text",5) == 0) {
    res = 0;
    *valeur = Text;
  }
  if (strncmp(var, "codeR_pac",10) == 0) {
    res = 0;
    if (cpt_securite)  *valeur=1;
    else *valeur=0;
  }


  return res;
}




// type 1
uint8_t requete_Set_appli (String param, float valf) 
{
  uint8_t res=1;
  int8_t val = round(valf);

    /*if (param == "consigne")     // Forcage consigne, rajouter duree
    {
      if ((valf >= 6.0) && (valf <= 22.0))  // 6°C à 22°C
      {
          fo_co = round(valf * 10);
          //fo_jus = 10;  // en minutes
          //preferences_nvs.putUChar("Cons", Consigne_G);
          res = 0;
      }
    }*/

    /*if (param == "vbatt")
    {
      res = 0;
      Vbatt_Th = valf;
      Vbatt_Th_I = 1;

      Serial.printf("Réception Vbatt Distante : %.2fV\n", Vbatt_Th);
    }*/


  return res;
}

// type 2
uint8_t requete_GetReg_appli(int reg, float *valeur)
{
  uint8_t res=1;

  
  if (reg == 41)  // registre 41 : canal WiFi actuel (dynamic)
  {
    res = 0;
    uint8_t current_channel;
    wifi_second_chan_t second;
    esp_wifi_get_channel(&current_channel, &second);
    *valeur = (float)current_channel;
  }

  return res;
}

// type 2
uint8_t requete_SetReg_appli(int param, float valeurf)
{
  int16_t valeur = int16_t(round(valeurf));
  uint8_t res = 1;

  return res;
}



// type 4
uint8_t requete_Get_String_appli(uint8_t type, String var, char *valeur)
{
  uint8_t res=1;
  int paramV = var.toInt();
  // valeur limité a 50 caractères
  
  if (paramV == 61)  // registre 61 : adresse MAC ce module
  {
    res = 0;
    strncpy(valeur, WiFi.macAddress().c_str(), 18);
  }
  if (paramV == 62)  // registre 62 : adresse MAC Gateway
  {
    res = 0;
    snprintf(valeur, 18,
           "%02X:%02X:%02X:%02X:%02X:%02X",
           mac_gw[0], mac_gw[1], mac_gw[2],
           mac_gw[3], mac_gw[4], mac_gw[5]);
  }

  return res;
}


uint8_t parseMacString(const char* str, uint8_t mac[6]) {
  int v[6];
  if (sscanf(str, "%x:%x:%x:%x:%x:%x",
             &v[0], &v[1], &v[2], &v[3], &v[4], &v[5]) != 6) {
    return false;
  }
  for (int i = 0; i < 6; i++) mac[i] = (uint8_t)v[i];
  return true;
}

// type 4
uint8_t requete_Set_String_appli(int param, const char *texte)
{
  uint8_t res=1;
  IPAddress ip;

    if (param == 62)  // registre 62 : adresse Mac Gateway
    {
      if (!parseMacString(texte, mac_gw))
      {
          Serial.println("MAC Serveur invalide");
      }
      else
      {
        preferences_nvs.putString("MacC", texte);
        res = 0;
      }
    }

  return res;
}

// type5 : reception message ACTION par uart ou par page web
uint8_t requete_action_appli(const char *reg, const char *data)
{
  uint8_t res=1;

  if (strcmp(reg, "Test1") == 0) 
    { 
      res=0; 
      requete_status(buffer_dmp, 0, 1);
      Serial.println(buffer_dmp);
    }

  if (strcmp(reg, "Tint") == 0) 
    { 
      res=0; 
      uint8_t Tint_erreur = lecture_Tint(&Tint,&Humid);
      Serial.println(Tint_erreur);
      Serial.println(Tint);
    }
  return res;
}

float absoluteHumidity(float T, float RH)
{
    return (13.247f * RH/100 * exp((17.67f * T) / (T + 243.5f)))
           / (273.15f + T);
}

// erreur :0:ok  sinon erreur 2 à 7
uint8_t lecture_Tint(float *mesure, float*humid)
{
  uint8_t Tint_erreur = 7;
  float valeur = 20;
  float valeur2 = 50;


    #ifdef Temp_int_DHT22
      //dht[0].begin();

      if (digitalRead(PIN_Tint22) == HIGH || digitalRead(PIN_Tint22) == LOW)
      {
        valeur =  dht[0].readTemperature();
        if (isnan(valeur))
        {
          valeur = 20.0;
          Tint_erreur = 6;
          Serial.println("---DHT:non numérique");
        }
        else
        {
          Tint_erreur=0;
        }
      }
      else
        Serial.println("---DHT:signal non stable!");
    #endif

    #ifdef Temp_int_HDC1080
      valeur = hdc1080.readTemperature();
      if (isnan(valeur) || (valeur>124)) {
        Serial.println("Reset i2c)");
        resetI2C(); 
        hdc1080.begin(0x40); 
        valeur = hdc1080.readTemperature();

        if (isnan(valeur) || (valeur>124)) {
          Serial.println("Recovery i2c)");
          i2cRecovery();
          hdc1080.begin(0x40); 
          valeur = hdc1080.readTemperature();
          if (isnan(valeur) || (valeur>124)) {
            valeur = 20.0;
            Tint_erreur = 4;
          } else  Tint_erreur=0;
        } else  Tint_erreur=0;

      } else {
        Tint_erreur=0;
      }
      valeur2 = hdc1080.readHumidity();
      if (log_detail>=3) Serial.printf("lecture HDC1080 - Temp: %.2f Humid:%.2f\n\r", valeur, valeur2);
      valeur = valeur + (float)(calib_temp-1000)/100.0;  // calibration temperature
      valeur2 = (valeur2 - (float)calib_hygro1/10.0) * (80.0-30.0) / ((float)calib_hygro2/10.0 - (float)calib_hygro1/10.0) + 30.0;  // calibration hygrométrie
      if (log_detail>=3) Serial.printf("lecture HDC1080M- Temp: %.2f Humid:%.2f\n\r", valeur, valeur2);

    #endif

    #ifdef Temp_int_DS18B20
      valeur = ds.getTemperature();
      Tint_erreur=0;
    #endif


  if (valeur > 50) Tint_erreur = 2;
  if (valeur < -20) Tint_erreur = 3;
  if (log_detail>=3) Serial.printf("lecture Tint : %.2f Humid:%.2f Tint_erreur:%i\n\r", valeur, valeur2, Tint_erreur);
  if (Tint_erreur) {
    valeur = 20.0;
    valeur2 = 50.0;
  }
  *mesure = valeur;
  *humid = valeur2;
  return Tint_erreur;
}



//mesure temperature exterieure
uint8_t lecture_Text(float *mesure) {
  uint8_t Text_erreur = 0;
  int16_t Val_Text = 1600;
  float valeur;

  #ifdef MODBUS
    Text_erreur = read_modbus(2, &Val_Text);  // registre 1-2-3 pour temp exterieure
    valeur = (float)Val_Text / 10;
  #else
    valeur = 18.0;

    /*#ifndef DEBUG_SANS_Sonde_Ext
        Val_Text = analogRead( PIN_Text );  // 0 à 4096
        //Serial.println(Val_Text);
      #endif*/
    // calibration
    // Text1:100(10°C) Text1Val:500
    // Text2:200(20°C) Text2Val:2000
    //valeur = ((float)(Text1Val-Val_Text)/(Text1Val-Text2Val)*(Text2-Text1) + Text1)/10;

    /*float Vmesure = ((float)Val_Text / resolutionADC) * 3.66;
      float Rntc = 15000 * Vmesure / (3.3 - Vmesure);  // Calcul de la résistance de la thermistance
      float T_kelvin = 1.0 / ((1.0 / 298.15) + (1.0 / TBeta) * log(Rntc / Therm0));    // Calcul de la température en Kelvin
      valeur = T_kelvin - 273.15;    // Conversion en °C */
    //Serial.printf("val_text:%i vmesure:%.3f rntc:%.0f T_kelvin:%.1f valeur:%.1f\n", Val_Text, Vmesure, Rntc, T_kelvin, valeur);
  #endif

  if ((valeur < -30.0) || (valeur > 60.0)) Text_erreur = 1;
  if (!Val_Text) Text_erreur = 2;

  *mesure = valeur;
  return Text_erreur;
}



uint8_t fetch_internet_temp() {

  uint8_t res=1;

  // 1. Vérifier si le réseau est disponible avant de commencer
  if (WiFi.status() != WL_CONNECTED) {
    // Si vous utilisez l'Ethernet, remplacez par le test approprié
    return res; 
  }

  HTTPClient http;

  // 2. Définir un timeout court (2000ms au lieu des 5-10s par défaut)
  http.setTimeout(2000); 

  char url[150];  // assez grand pour contenir toute l'URL
  sprintf(url, "http://api.open-meteo.com/v1/forecast?latitude=%s&longitude=%s&current=temperature_2m", LATITUDE, LONGITUDE);


  if (http.begin(url)) {
    int httpCode = http.GET();
    if (httpCode == 200) {
      String payload = http.getString();

      // pour éviter les warnings de la librairie ArduinoJson sur les anciennes versions de l'ESP32
      #pragma GCC diagnostic push
      #pragma GCC diagnostic ignored "-Wdeprecated-declarations"
      DynamicJsonDocument doc(512);
      #pragma GCC diagnostic pop

      DeserializationError error = deserializeJson(doc, payload);
      
      if (!error) {
        float temp = doc["current"]["temperature_2m"] | NAN;
        if (!isnan(temp) && temp > -50.0 && temp < 60.0)
        {
          res = 0;
          Text = temp;
          //Serial.printf("Météo Garches : %.1f°C\n", Text);
          uint32_t mil = millis();
          if (mil - last_remote_Text_time > 35*60*1000) // le precedent message est vieux de plus de 35 minutes
            err_Text++;
          last_remote_Text_time = mil;
          cpt24_Text++;
          tempE_moy24h += Text;
        }
      } else {
        Serial.printf("Erreur parsing JSON Météo : %s\n", error.c_str());
      }
    } else {
      Serial.printf("Erreur HTTP Météo (%d) : %s\n", httpCode, http.errorToString(httpCode).c_str());
    }
    http.end();
  }
  return res;
}

uint8_t enreg_valeur()
{
  unsigned long currentMillis = millis();  // en minutes

  if (!esp_now_actif) return 1;

  if (cpt_nb_val >= NB_VAL_TAB) cpt_nb_val = 0;
  valTemp[cpt_nb_val] = (uint16_t)((Tint+40) * 100);
  valHum[cpt_nb_val] = (uint16_t)(Humid * 100);
  valEcart[cpt_nb_val] = (currentMillis - Mesure_min_prec)/60000;
  
  cpt_nb_val++;
  uint8_t envoi=0;

  if (cpt_nb_val >= Capt_nb_val_max) envoi=1;
  if (freq_envoi)  // envoi si temps total dépassé
  {
    if ((currentMillis - envoi_min_prec)/60000 >= Capt_tps_total_max) envoi=1;
  }

  if (envoi)
  {

    Message_EspNow message;

    message.destinataire = SERVER_ADD | 0x80;  // 0x80 = message hexa
    message.emetteur = ADDRESS;
    message.code = 'C';
    message.code2 = 'T';

    message.payload[0] = cpt_nb_val;
    
    uint8_t pos = 1;
    for (uint8_t cpt = 0; cpt < cpt_nb_val; cpt++)
    {
      payloadWrite(message.payload, pos, valTemp[cpt]);  
      payloadWrite(message.payload, pos, valHum[cpt]);
      payloadWrite(message.payload, pos, valEcart[cpt]);
    }
    // Taille réelle du message envoyé
    message.longueur = 5 + pos - 3;

    uint8_t result = envoi_data_gateway(message);


    if (result == ESP_OK) {
        Serial.printf("✅ %d valeurs Temp-HR-Ecart envoyées\n",  message.payload[0]);
        for (uint8_t cpt = 0; cpt < message.payload[0]; cpt++)
        {
          Serial.printf("   %.2f°C %.2f%% %d min\n", valTemp[cpt]/100.0-40, valHum[cpt]/100.0, valEcart[cpt]);
        }
    } else {
        Serial.printf("❌ Erreur envoi ESP-NOW : %d\n", result);
        activation_writelog();
        writeLog('E', 8, valTemp[0]/10,  valHum[0]/10, "Esp_now");

        return 1;
    }
    cpt_nb_val=0;
    envoi_min_prec = currentMillis;  // mettre à jour le temps du dernier envoi
    return 0;
  }
  return 0;
}

uint8_t envoi_valeur_instant(float Tint, float Humid, float HA)
{
  if (!esp_now_actif) return 0;

  Message_EspNow message;

  message.destinataire = SERVER_ADD | 0x80;  // 0x80 = message hexa
  message.emetteur = ADDRESS;
  message.code = 'C';
  message.code2 = 'I';

  uint8_t pos = 0;
  payloadWrite(message.payload, pos, (uint16_t)(Tint * 100));
  payloadWrite(message.payload, pos, (uint16_t)(Humid * 100));
  payloadWrite(message.payload, pos, (uint16_t)(HA * 100));

  // Taille réelle du message envoyé
  message.longueur = 8;

  uint8_t result = envoi_data_gateway(message);
  return result;
}

void envoi_temp_hygro()
{
  lecture_Tint(&Tint, &Humid);
  if (log_detail>=2) Serial.printf("milli H2: %lu\n", millis());

  float HA = absoluteHumidity(Tint, Humid);
  if (log_detail>=2) Serial.printf("Temp int:%.2f Humid:%.2f HA:%.2f\n\r", Tint, Humid, HA); 
  envoi_valeur_instant(Tint, Humid, HA);
}


void event_cycle()  // toutes les 15 minutes  (Power on, Timer on, inconnu)
{
  unsigned long currentMillis = millis();  
  
    uint8_t i;
    // chaque 5/15 minutes
    for (i = NB_VAL_TAB - 1; i; i--) {
        Nb_PI[i]= Nb_PI[i - 1];
    }    
    Nb_PI[0] = compteur_detection;
    compteur_detection=0;

  // Récupération de la température extérieure par internet
  //fetch_internet_temp();
  Text = 0;

  // lecture temp-humi
  uint8_t err_Tint = lecture_Tint(&Tint, &Humid);
  if (err_Tint)
    log_erreur(Code_erreur_Tint, err_Tint, 1);
  else
    Serial.printf("Temp int:%.2f Humid:%.2f\n\r", Tint, Humid); 

  float HA = absoluteHumidity(Tint, Humid);

  float tempI_moy15m, tempE_moy15m, HA_moy15m, Hum_moy15m, PIR_moy15m;
  uint8_t cpt15_Tint, cpt15_Text, cpt15_HA, cpt15_Hum, cpt15_PIR;

  tempI_moy15m += Tint;
  cpt15_Tint++;
  tempE_moy15m += Text;
  cpt15_Text++;
  HA_moy15m += HA;
  cpt15_HA++;
  Hum_moy15m += Humid;
  cpt15_Hum++;
  PIR_moy15m += compteur_detection_1h;
  cpt15_PIR++;

  // chaque 15 minutes
  compteur_graph++;
  if (compteur_graph >= skip_graph)  // 1 valeur sur x
  {
    int16_t tempI_arrondi = 200;
    int16_t tempE_arrondi = 150;
    int16_t HA_arrondi = 10;
    int16_t Hum_arrondi = 500;
    int16_t PIR_arrondi = 0;

     if (cpt15_Tint > 0) {  tempI_arrondi = round(tempI_moy15m / cpt15_Tint * 100);  }
     if (cpt15_Text > 0) {  tempE_arrondi = round(tempE_moy15m / cpt15_Text * 100); }
     if (cpt15_HA > 0) {    HA_arrondi = round(HA_moy15m / cpt15_HA * 100); }
     if (cpt15_Hum > 0) {   Hum_arrondi = round(Hum_moy15m / cpt15_Hum * 100); }
     if (cpt15_PIR > 0) {   PIR_arrondi = round(PIR_moy15m / cpt15_PIR * 10); }

    compteur_graph = 0;
    for (i = NB_Val_Graph - 1; i; i--) {
      graphique[i][0] = graphique[i - 1][0];
      graphique[i][1] = graphique[i - 1][1];
      graphique[i][2] = graphique[i - 1][2];
    }
    graphique[0][0] = tempI_arrondi; // 20°C => 2000
    graphique[0][1] = selec_graph(GRAPH2, HA_arrondi, Hum_arrondi, PIR_arrondi);
    graphique[0][2] = selec_graph(GRAPH3, HA_arrondi, Hum_arrondi, PIR_arrondi);

    if (compteur_detection_1h>1)  // au moins 2
    {
      uint8_t tot = (uint8_t)compteur_detection_1h;
      if (compteur_detection_1h > 255) tot = 255;
      writeLog('D', 1, tot, 0, "PIR 1h");
    }
    compteur_detection_1h = 0;

    if (action_envoi) 
    {
      if (freq_envoi)  // envoi periodique activé
      {
        cpt_mesure++;
        if (cpt_mesure >= freq_envoi)  // mesure toutes les x valeurs (x*skip_graph*15 minutes)
        {
          Serial.printf("MMesure périodique toutes les %i valeurs (cpt_mesure=%i)\n", freq_envoi, cpt_mesure);
          cpt_mesure=0;
          enreg_valeur();  // enreg puis envoi  par ESP-NOW
          Mesure_min_prec = currentMillis;
          TintPrec = Tint;
        }
      }
      else // envoi lorsque les valeurs évoluent
      {
        uint8_t enreg=0;
        if (Tint - TintPrec > Capt_seuil_temp) enreg=1;
        if (TintPrec - Tint > Capt_seuil_temp) enreg=1;
        if ((currentMillis - Mesure_min_prec)/60000 > Capt_tps_max) enreg=1;
        if (enreg) {
          Serial.printf("Envoi : %i minutes (cpt_nb_val=%i)\n", Capt_tps_max, cpt_nb_val);
          enreg_valeur();  // enreg puis envoi  par ESP-NOW
          TintPrec = Tint;
          Mesure_min_prec = currentMillis;
        }
      }
    }
    tempI_moy15m = 0;
    tempE_moy15m = 0;
    HA_moy15m = 0;
    Hum_moy15m = 0;
    PIR_moy15m = 0;
    cpt15_Tint = 0;
    cpt15_Text = 0;
    cpt15_HA = 0; 
    cpt15_Hum = 0;
    cpt15_PIR = 0;
  }
  tempI_moy24h += Tint;
  cpt24_Tint++;
  tempE_moy24h += Text;
  cpt24_Text++;
  HA_moy24h += HA;
  cpt24_HA++;
  Hum_24h += Humid;
  cpt24_Hum++;
  PIR_24h += compteur_detection_1h;
  cpt24_PIR++;

  //Serial.printf("fin cycle :reveil:%i cpt:%i %i tint:%i 24h:%i\n\r", type_reveil, compteur_graph, skip_graph, graphique[0][0], compteur_24h);
  
  if (type_reveil != 10)      // si diff de toujours actif => compteur 24h
  {
    compteur_24h++;
    if (compteur_24h >= 24*60/periode_cycle) // )  // toutes les 24h
    {
      Serial.println("24h");
      activation_writelog();
      enreg_24h(1);  // et envoi si actif
      compteur_24h=0;
    }
  }
}



float readBatteryVoltage() {
  // Lecture ADC (0-4095) sur PIN_Vbatt
  // Sur ESP32 DevKit V1, l'ADC est calibré par défaut
  int raw = analogRead(PIN_Vbatt);
  float voltage = 0.0;  
  // Conversion:
  // raw / 4095.0 * 3.3V (tension ref approx) * 2 (pont diviseur) * 1.1 (facteur corection empirique souvent nécessaire sur ESP32)
  // On commence sans facteur 1.1 pour tester
  #ifdef ESP32_Fire2
     voltage = (raw / 4095.0) * 3.3 * 2.5; 
  #endif

  #ifdef ESP32_uPesy
     voltage = (raw / 4095.0) * 3.3 * 1.411 ;
  #endif

  return voltage;
}

#ifdef ESP_TJ_ACTIF
// Callback reception ESP-NOW
void OnDataRecv(const uint8_t *mac, const uint8_t *incomingData, int len) {
//void OnDataRecv(const esp_now_recv_info_t *info, const uint8_t *data, int len) {
//void OnDataRecv(const esp_now_peer_info_t * info, const uint8_t *incomingData, int len) {
  // 🔍 DIAGNOSTIC: Afficher infos de réception
  Serial.println("\n📥 ========== RECEPTION ESP-NOW ==========");
  for (int i = 0; i < 6; i++) {
        Serial.printf("%02X", mac[i]);
        if (i < 5) Serial.print(":");
    }
  /*Serial.printf("   Source MAC: %02X:%02X:%02X:%02X:%02X:%02X\n",
                info->src_addr[0], info->src_addr[1], info->src_addr[2],
                info->src_addr[3], info->src_addr[4], info->src_addr[5]);*/
  
  // Afficher le canal WiFi actuel
  uint8_t current_channel;
  wifi_second_chan_t second;
  esp_wifi_get_channel(&current_channel, &second);
  Serial.printf("   Canal WiFi actuel: %d\n", current_channel);
  Serial.printf("   Taille reçue: %d octets\n", len);
  
  Message_EspNow receivedMessage;
  memcpy(&receivedMessage, incomingData, sizeof(receivedMessage));

  Serial.print("   Type: "); Serial.print(receivedMessage.type);
  Serial.print(" | Valeur: "); Serial.println(receivedMessage.valuef);
  Serial.println("=========================================\n");

  if (receivedMessage.type == 1) { // Temperature
    float Trecue = receivedMessage.valuef;
    if ((Trecue > -10.0) && (Trecue < 49.99f)) 
    {
      Tint = Trecue;
      if ((Trecue < 24.99) || (Trecue > 25.01))
      {
        unsigned long mil = millis();
        if (mil - last_remote_Tint_time > 25*60*1000) // le precedent message est vieux de plus de 25 minutes
          err_Tint++;
        last_remote_Tint_time = mil;
        cpt24_Tint++;
        tempI_moy24h += Tint;
      }
    }
    Serial.printf("✅ Tint mise à jour: %.2f°C\n", Tint);
  }
  else if (receivedMessage.type == 2) { // Batterie
    Vbatt_ESP = receivedMessage.valuef;
    Serial.printf("✅ Vbatt_Th mise à jour: %.2fV\n", Vbatt_ESP);
  }
  else {
    Serial.printf("⚠️ Type de message inconnu: %d\n", receivedMessage.type);
  }
}
#endif


// envoie data à la gateway par ESP_now
uint8_t envoi_data_gateway(Message_EspNow mess_esp)
{

  if ((mac_gw[0] || mac_gw[3] || mac_gw[4]) && (esp_now_actif==1))
  {
    // Initialisation WiFi en mode Station (nécessaire pour ESP-NOW)

 // Ne change le mode que si nécessaire
    if (WiFi.getMode() != WIFI_STA &&  WiFi.getMode() != WIFI_AP_STA)
    {
        WiFi.mode(WIFI_STA);
        Serial.println("WiFi mode set to STA for ESP-NOW");
    }
    //WiFi.disconnect();

    if (esp_now_init() != ESP_OK) {
      Serial.println("Error initializing ESP-NOW");
      ESP.restart();
    }

    esp_now_register_send_cb(OnDataSent);

    // Préparation du Peer (Chaudière)
    esp_now_peer_info_t peerInfo;
    memset(&peerInfo, 0, sizeof(peerInfo)); // Initialisation complète à zéro
    memcpy(peerInfo.peer_addr, mac_gw, 6);
    peerInfo.channel = 0; // Le canal sera défini avant l'ajout
    peerInfo.encrypt = false;
    peerInfo.ifidx = WIFI_IF_STA; // Interface WiFi Station (OBLIGATOIRE)

    // 🚀 OPTION 1 : Forcer le canal connu (plus rapide et économe en énergie)
    // Si vous connaissez le canal de votre routeur, décommentez ces lignes :
    /*
    Serial.printf("🎯 Forçage canal %d (défini dans variables.h)\n", WIFI_CHANNEL);
    esp_wifi_set_promiscuous(true);
    esp_wifi_set_channel(WIFI_CHANNEL, WIFI_SECOND_CHAN_NONE);
    esp_wifi_set_promiscuous(false);
    last_wifi_channel = WIFI_CHANNEL; // Pour la prochaine fois
    */

    // 🔍 OPTION 2 : Scan robuste des canaux (si le canal n'est pas connu ou change)

    Serial.printf("🔍 Scan de 13 canaux (priorité: canal %d)\n", last_wifi_channel);
    uint8_t deliverySuccess = false;
    uint8_t current_channel;
    if (!last_wifi_channel || last_wifi_channel>13) last_wifi_channel=1;  // si corrompu : channel 1

    if (etat_now==0)  // encore aucun envoi essayé
    {
      for (uint8_t k = 0; k < 13; k++)  // => channel 1 à 13
      {
        current_channel = k + last_wifi_channel;
        if (current_channel > 13) current_channel -= 13;
        deliverySuccess = envoi_now(current_channel, &peerInfo, &mess_esp);
        if (deliverySuccess) break;
      }
      if (deliverySuccess) etat_now=2;
      else etat_now=1;
    }
    else if (etat_now==2)  // essai precedent reussi
    {
      deliverySuccess = envoi_now(last_wifi_channel, &peerInfo, &mess_esp);
      if (!deliverySuccess) etat_now=4; // prochain essai sur le meme channel+ scan
    }
    else if (etat_now==1 || etat_now==3)  // essai precedent raté
    {
      deliverySuccess = envoi_now(last_wifi_channel, &peerInfo, &mess_esp);
      if (deliverySuccess) etat_now=2; 
      else 
      {
        // prochain essai sur le channel suivant
        last_wifi_channel++;
        if (last_wifi_channel > 13) last_wifi_channel=1;
      }
    }
    else if (etat_now==4)  // envoi precedent raté mais celui d'avant réussi
    {
      for (uint8_t k = 0; k < 14; k++)  // => channel 1 à 13 et 1 de plus
      {
        current_channel = k + last_wifi_channel;
        if (current_channel > 13) current_channel -= 13;
        deliverySuccess = envoi_now(current_channel, &peerInfo, &mess_esp);
        if (deliverySuccess) break;
      }
      if (deliverySuccess) etat_now=2;
      else etat_now=3;
    }
    else etat_now=0; // si etat_now corrompu, remise à 0

    Serial.printf("etat_now:%i\n\r", etat_now);
    return 1-deliverySuccess;
    
  }
  else
    Serial.println("Adresse Mac gateway nulle");

  return 2;
}

uint8_t envoi_now(uint8_t channel, esp_now_peer_info_t * peerInfo, Message_EspNow * message)
{
  uint8_t result = false;

  // Fixer le canal
  Serial.printf("\n--- Essai canal %d ---\n", channel);
  esp_wifi_set_promiscuous(true);
  esp_wifi_set_channel(channel, WIFI_SECOND_CHAN_NONE);
  esp_wifi_set_promiscuous(false);
  
  // Vérifier que le canal a bien été changé
  uint8_t actual_channel;
  wifi_second_chan_t second;
  esp_wifi_get_channel(&actual_channel, &second);
  
  if (actual_channel != channel)
  {
    Serial.printf("⚠️ Échec changement canal (demandé:%d, actuel:%d)\n", channel, actual_channel);
    delay(100); // Attendre un peu plus
    esp_wifi_set_channel(channel, WIFI_SECOND_CHAN_NONE);
    esp_wifi_get_channel(&actual_channel, &second);
    Serial.printf("   2ème tentative: canal actuel=%d\n", actual_channel);
  } else {
    //Serial.printf("✅ Canal changé: %d\n", actual_channel);
  }
  
  delay(50); // Délai pour stabilisation du canal

  // Ajouter le peer sur ce canal
  if (esp_now_is_peer_exist(mac_gw)) {
    esp_now_del_peer(mac_gw);
  }
  peerInfo->channel = actual_channel; // Utiliser le canal réel
  if (esp_now_add_peer(peerInfo) != ESP_OK){
    Serial.println("❌ Échec ajout peer");
  }
  //Serial.println("✅ Peer ajouté");

  // Envoi Message
  
  // 🔍 DIAGNOSTIC: Afficher les infos avant envoi
  /*Serial.printf("📤 Tentative envoi sur canal %d vers MAC: %02X:%02X:%02X:%02X:%02X:%02X\n",
                actual_channel,
                mac_gw[0], mac_gw[1], mac_gw[2],
                mac_gw[3], mac_gw[4], mac_gw[5]);*/
  
  ackReceived=0;
  ackChannel = -1;
  esp_err_t resulta = esp_now_send(mac_gw, (uint8_t *) message, message->longueur+3);

  // impression du message envoyé pour diagnostic
  for (int i = 0; i < message->longueur+3; i++) {
    Serial.printf("%02X ", ((uint8_t*)message)[i]);
  }

  if (resulta == ESP_OK)
  {
    //Serial.printf("Envoye sur canal %d\n", actual_channel);

    // attendre la réponse max 100 ms
    int wait = 0;
    while (!ackReceived && wait < 10) { // 10 * 10ms = 100ms
        delay(10);
        wait++;
    }

    if (ackReceived) // canal trouvé
    {
      result = true; 
      Serial.println("✅ Ack Recu");
      if (last_wifi_channel != actual_channel)
      {
        last_wifi_channel = actual_channel;
      }
    }
  }
  else Serial.println("❌ Echec d'envoi");

  return result;
}


